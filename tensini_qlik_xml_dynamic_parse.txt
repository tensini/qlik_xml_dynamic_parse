/*
 Link: https://github.com/tensini/qlik_xml_dynamic_parse
 Created by http://linkedin.com/in/tensini
*/


// Config:
SET vXML_Column    = "@1";
SET vXML_Delimiter = ";";
SET vXML_File      = "[lib://Desktop/example.xml](txt, codepage is 1252, no labels, delimiter is '$(vXML_Delimiter)', msq)";



// Identify how many valid fields exist in the XML
TMP_Fields_Qty:
LOAD
 SubStringCount($(vXML_Column),'</') as Q_Fields
FROM $(vXML_File);

LET vXML_Fields_Qty = Peek('Q_Fields');

DROP Table TMP_Fields_Qty;



// Clear the tag from the fields and get their content.
FOR i =  1 to $(vXML_Fields_Qty)
 
 Data:
 LOAD 
  Replace( TextBetween( SubField( Replace([$(vXML_Column)], '/', ''), '><', $(i)), '', '>'), '<', '') as XML_ColumnName,
  TextBetween( SubField([$(vXML_Column)], '><', $(i)), '>', '<')                                      as XML_Data 
 FROM $(vXML_File)
;
	
NEXT // FOR i =  1 to $(vXML_Fields_Qty)



// Eliminates fields that have no content or are used as a hierarchy.
Fields:
LOAD Distinct
 Concat(distinct '[' & XML_ColumnName & ']', ',') as Fields
RESIDENT [Data]
 Where Len(Trim(XML_Data)) > 0;

LET vXML_Fields = Peek('Fields',0,'Fields');



// Fields with no content are considered to be hierarchy fields and possibly their source table.
// You can verify this is correct by manually loading XML and looking at the table name.
Tables:
LOAD Distinct
 Concat(distinct XML_ColumnName, '/')             as Tables
RESIDENT [Data]
 Where Len(Trim(XML_Data)) = 0;

LET vXML_Tables = Peek('Tables',0,'Tables');


// Maintenance
DROP Tables
 Data,
 Fields,
 Tables
 ;



// Simulating an XML file and keeping all fields on a single line
XMLTABLE:
LOAD
 Concat(Column, '', RecNo) as XMLFIELD
;
LOAD
 RecNo()                   as RecNo,
 [$(vXML_Column)]          as Column
FROM $(vXML_File);



// Specific load in simple XML mode
Table:
LOAD
  $(vXML_Fields)
FROM_FIELD('XMLTABLE','XMLFIELD') (xmlSimple, table is [$(vXML_Tables)]);



// Maintenance
DROP Table XMLTABLE;